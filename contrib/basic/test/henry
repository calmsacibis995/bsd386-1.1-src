
READY

 10 READ B
 20 PRINT "BANK STATEMENT"
 30 PRINT "STARTING BALANCE ";B
 40 READ X
 50 IF X=0 THEN 500
 60 IF X>0 THEN 200
 70 REM -- IT IS A CHECK
100   PRINT "CHECK ";-X
110   LET B=B+X-.10
120   IF B>=0 THEN 40
130   REM ***OVERDRAFT***
140     PRINT "***OVERDRAFT***"
150     LET B=B-5
160     GOTO 40
200   PRINT "DEPOSIT ";X
210   LET B=B+X
220   GOTO 40
500 PRINT "NEW BALANCE ";B
510 DATA 278.15
520 DATA -55.11,-104.14,35,-211.63,787.93,-78.52
530 DATA 0
999 END

READY

  10 REM DEALING BRIDGE HANDS
  20 REM WRITTEN BY         HENRY D. SHAPIRO
  30 REM                    CS 150  SEC 007
  40 REM                    10/26/81
  50
  60 REM VARIABLE DICTIONARY
  70 REM   D(52) - HOLDS THE DECK OF CARDS
  80 REM   N(13) - HOLDS NORTH HAND
  90 REM   E(13) - HOLDS EAST HAND
 100 REM   S(13) - HOLDS SOUTH HAND
 110 REM   W(13) - HOLDS WEST HAND
 120 REM   C     - NUMBER OF CARDS LEFT IN DECK THAT ARE UNDEALT
 130 REM   I     - WHICH CARD WE ARE DEALING NOW (FIRST, SECOND,..,THIRT
EENTH)
 140 REM   J     - 1,2,3,4 FOR NORTH, EAST, SOUTH, AND WEST
 150 REM   K     - LOCATION WITHIN DECK OF CARD WE ARE DEALING (TRICKY!!
)
 160 REM   X     - A CARD (1 THROUGH 52)
 170 REM   Y     - THE SUIT (SPADE=4, HEART=3, DIAMOND=2, CLUB=1)
 180 REM   Z     - VALUE OF CARD (1 THROUGH 13)
 190
 200 REM CARDS ARE NUMBERED 1 THROUGH 52.  ACES ARE 1, 14, 27, 40, TWOS
ARE
 210 REM 2, 15, 28, 41, ... , KINGS ARE 13, 26, 39, 52.
 220 REM SPADES ARE 40-52, HEARTS ARE 27-39, DIAMONDS ARE 14-26, AND CLU
BS
 230 REM ARE 1-13.
 240
 250 DIM D(52),N(13),E(13),S(13),W(13)
 260
 270 REM SET UP DECK INITIALLY (SORTED!)
 280 FOR C=1 TO 52
 290   LET D(C)=C
 300 NEXT C
 310
 320 LET I=1   REM GET READY TO DEAL CARD 1
 330 LET J=1   REM TO PLAYER NORTH
 340 FOR C=52 TO 1 STEP -1
 350   LET K=INT(C*RND)+1   REM RANDOMLY SELECTED SLOT
 360   GOTO 370,400,430,460 ON J
 370   LET N(I)=D(K)
 380   LET J=2
 390   GOTO 510
 400   LET E(I)=D(K)
 410   LET J=3
 420   GOTO 510
 430   LET S(I)=D(K)
 440   LET J=4
 450   GOTO 510
 460   LET W(I)=D(K)
 470   LET J=1
 480   LET I=I+1    REM AND GET READY TO DEAL THE NEXT CARD TO EACH PLAY
ER
 490
 500   REM FILL IN THE HOLE CREATED BY DEALING CARD K
 510   LET D(K)=D(C)
 520 NEXT C
 530
 540 REM HANDS ARE DEALT
 550 REM PRINT THEM (MORE OR LESS) NEATLY
 560 FOR J=1 TO 4
 570   GOTO 580,600,620,640 ON J
 580   PRINT "NORTH:  ";
 590   GOTO 650
 600   PRINT "EAST:   ";
 610   GOTO 650
 620   PRINT "SOUTH:  ";
 630   GOTO 650
 640   PRINT "WEST:   ";
 650   FOR I=1 TO 13
 660     GOTO 670,690,710,730 ON J
 670     LET X=N(I)
 680     GOTO 740
 690     LET X=E(I)
 700     GOTO 740
 710     LET X=S(I)
 720     GOTO 740
 730     LET X=W(I)
 740     LET Y=INT((X-1)/13)+1  REM SPADE=4, HEART=3, DIAMOND=2, CLUB=1
 750     LET Z=X-13*(Y-1)       REM VALUE OF CARD
 760     GOTO 770,790,810,830 ON Y
 770     PRINT "   C ";
 780     GOTO 840
 790     PRINT "   D ";
 800     GOTO 840
 810     PRINT "   H ";
 820     GOTO 840
 830     PRINT "   S ";
 840     GOTO 850,870,870,870,870,870,870,870,870,900,930,950,970 ON Z
 850     PRINT "A";
 860     GOTO 980
 870     PRINT USING 880,Z
 880     FORM PIC(#)
 890     GOTO 980
 900     PRINT USING 910,Z
 910     FORM PIC(##)
 920     GOTO 980
 930     PRINT "J";
 940     GOTO 980
 950     PRINT "Q";
 960     GOTO 980
 970     PRINT "K";
 980   NEXT I
 990   REM FORCE THE CARRIAGE RETURN AT THE END OF THE HAND
1000   PRINT
1010 NEXT J
1020 END
